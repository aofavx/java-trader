

buildscript {
    ext {
        springVersion = '5.3.2'
        springBootVersion = '2.4.1'
        jettyVersion = '9.4.35.v20201120'
    }
    repositories {
        mavenLocal()
        maven {
            url 'https://maven.aliyun.com/repository/central'
        }
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring'
        }
        maven {
            url 'https://maven.aliyun.com/repository/spring-plugin'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        mavenCentral()
    }
    dependencies {
        classpath(
        "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: 'java'
    apply plugin: "maven"

    group = 'trader'
    version = '1.0.0'
    ext {
        jtraderCommonVersion = '1.0.0.0-20190528'
        jctpLinuxVersion = '6.3.19_20200106-linux_x64'

        gsonVersion = '2.8.6'
        guavaVersion = '30.1-jre'
        juntVersion = '4.13'
        mysqlVersion = '8.0.19'
        ta4jVersion = '0.13'
        commonsTextVersion = '1.8'
        commonsCliVersion = '1.4'
        commonsVFSVersion = '2.7.0'
        jsoupVersion = '1.13.1'
        httpcomponentVersion = '4.5.13'
        jdom2Verson = '2.0.6'
        slf4jVersion = '1.7.30'
        fastUtilVersion = '8.4.4'
        rocksdbVersion = '6.13.3'
        classgraphVersion = '4.8.93'
        disruptorVersion = '3.4.2'
        groovyVersion = '3.0.7'
        swaggerVersion = '2.9.2'
        h2Version = '1.4.200'
        sqliteVersion = '3.32.3.2'
        zip4jVersion = '2.6.4'
        tikaVersion='1.25'
    }
    ext.gitRevision = "git rev-parse --short HEAD".execute().text.trim()

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    jar {
        manifest {
            attributes(
                "Implementation-Title": project.group+"."+archivesBaseName,
                "Implementation-Version": project.version,
                'Built-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Git-Revision': gitRevision
                )
        }
    }

    repositories {
        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        mavenCentral()
        jcenter()
    }

    //Export all classpath entries
    eclipse {
        classpath {
            downloadSources=true
            file {
                withXml {
                    xml -> xml.asNode().classpathentry.findAll{
                        it.@kind == 'lib' || it.@kind == 'src'
                    }.each{
                        it.@exported = 'true'
                    }
                }
            }
        }
    }
}

ext{
    traderHome = System.getenv('TRADER_HOME')
    if ( traderHome==null ){
        traderHome = System.getProperty('user.home')+'/traderHome'
        if( System.getProperty("os.name").indexOf("Windows")>=0 ){
            traderHome = "C:/traderHome"
        }
    }
}

task localDeploy() {

    dependsOn ':plugin-jctp-6.3.13-linux_x64:localDeploy'
    dependsOn ':plugin-jctp-6.3.13-win32_x64:localDeploy'
    dependsOn ':plugin-jctp-6.3.13-win32_x86:localDeploy'
    dependsOn ':plugin-jctp-6.3.15-linux_x64:localDeploy'
    dependsOn ':plugin-jctp-6.3.15-win32_x64:localDeploy'
    dependsOn ':plugin-jctp-6.3.15-win32_x86:localDeploy'
    dependsOn ':plugin-jctp-6.3.19-p1-linux_x64:localDeploy'
    dependsOn ':plugin-jctp-6.3.19-p1-win32_x64:localDeploy'
    dependsOn ':trader-services:localDeploy'
    dependsOn ':trader-broker:localDeploy'

}
